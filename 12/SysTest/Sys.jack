// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Memory.init();
        do Math.init();
        do Screen.init();
        do Output.init();
        do Keyboard.init();
        do Main.main();
        do Sys.halt();
    }

    /** Halts the program execution. */
    function void halt() {
        while ( true ) {}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int innerCycles, outerCycles, clockConstant;
        let outerCycles = 0;
        let clockConstant = 100; // Machine dependent constant to match duration to run time in ms.

        if ( duration < 0 ) {
            do Sys.error(1); // duration must be positive
        }

        while ( outerCycles < duration ) {
            let innerCycles = 0;
            while ( innerCycles < clockConstant ) {
                let innerCycles = innerCycles + 1;
            }
            let outerCycles = outerCycles + 1;
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Output.printString("ERR");
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }
}
