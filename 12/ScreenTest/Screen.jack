// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on
 * the screen is indexed (0,0).
 */
class Screen {
    static Array screen, powersOfTwo;
    static int numRows;
    static boolean color;

    /** Initializes the Screen. */
    function void init() {
        var int i, n;

        let screen = 16384;
        let numRows = 8192;
        let color = true;

        /** Could import powersOfTwo values from custom Math library function
         *  but re-implementing here to enable library to use built-in Math library. */
        let n = 16;
        let i = 1;
        let powersOfTwo = Array.new(n);
        let powersOfTwo[0] = 1;

        while (i < n) {
            let powersOfTwo[i] = powersOfTwo[i - 1] + powersOfTwo[i - 1];
            let i = i + 1;
        }

        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int row;
        let row = 0;
        while ( row < numRows ) {
            let screen[row] = false;
            let row = row + 1;
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int address, bit, mask;

        if ( (x < 0 ) | (x > 511) | (y < 0) | (y > 255) ) {
            do Sys.error(7); // illegal pixel coordinates
        }

        let address = (32 * y) + (x / 16);
        let bit = x - ((x/16)*16);
        let mask = powersOfTwo[bit];
        if ( color ) {
            let screen[address] = screen[address] | mask;
        }
        else {
            let screen[address] = screen[address] & ~mask;
        }

        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int dx, dy, x, y, a, b, diff;
        var boolean up;

        if ( (x1 < 0 ) | (x1 > 511) | (y1 < 0) | (y1 > 255) | (x2 < 0 ) | (x2 > 511) | (y2 < 0) | (y2 > 255) ) {
            do Sys.error(8); // illegal line coordinates
        }

        if ( x2 > x1 ) {
            let dx = x2 - x1;
            let x = x1;
            if ( y2 > y1 ) {
                let dy = y2 - y1;
                let y = y1;
                let up = true;
            }
            else {
                if ( y2 < y1 ) {
                    let dy = y1 - y2;
                    let y = y1;
                    let up = false;
                }
                else {
                    do Screen.drawHorLine(y1, x1, x2);
                }
            }
        }
        else {
            if ( x1 > x2 ) {
                let dx = x1 - x2;
                let x = x2;
                if ( y1 > y2 ) {
                    let dy = y1 - y2;
                    let y = y2;
                    let up = true;
                }
                else {
                    if ( y1 < y2 ) {
                        let dy = y2 - y1;
                        let y = y2;
                        let up = false;
                    }
                    else {
                        do Screen.drawHorLine(y1, x1, x2);
                    }
                }
            }
            else {
                do Screen.drawVertLine(x1, y1, y2);
            }
        }

        let a = 0;
        let b = 0;
        let diff = 0;

        while ( ~(a > dx) & ~(Math.abs(b) > dy) ) {
            do Screen.drawPixel(x+a, y+b);
            // Compute a and b for next pixel
            if ( diff < 0 ) {
                let a = a + 1;
                let diff = diff + dy;
            }
            else {
                if ( up ) {
                    let b = b + 1;
                }
                else {
                    let b = b - 1;
                }
                let diff = diff - dx;
            }
        }

        return;
    }

    /** Draws a vertical line from pixel (x,y1) to pixel (x,y2), using the current color. */
    function void drawVertLine(int x, int y1, int y2) {
        var int dy, y, b;

        if ( y2 > y1 ) {
            let dy = y2 - y1;
            let y = y1;
        }
        else {
            let dy = y1 - y2;
            let y = y2;
        }

        let b = 0;
        while ( ~(b > dy)) {
            do Screen.drawPixel(x, y+b);
            let b = b + 1;
        }

        return;
    }

    /** Draws a horizontal line from pixel (x1,y) to pixel (x2,y), using the current color. */
    function void drawHorLine(int y, int x1, int x2) {
        var int dx, x, a;

        if ( x2 > x1 ) {
            let dx = x2 - x1;
            let x = x1;
        }
        else {
            let dx = x1 - x2;
            let x = x2;
        }

        let a = 0;
        while ( ~(a > dx)) {
            do Screen.drawPixel(x+a, y);
            let a = a + 1;
        }

        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int dx, dy, x, y, a;

        if ( (x1 < 0 ) | (x1 > 511) | (y1 < 0) | (y1 > 255) | (x2 < 0 ) | (x2 > 511) | (y2 < 0) | (y2 > 255) ) {
            do Sys.error(9); // illegal rectangle coordinates
        }

        if ( x1 < x2 ) {
            let dx = x2 - x1;
            let x = x1;
        }
        else {
            let dx = x1 - x2;
            let x = x2;
        }
        if ( y1 < y2 ) {
            let dy = y2 - y1;
            let y = y1;
        }
        else {
            let dy = y1 - y2;
            let y = y2;
        }

        let a = 0;
        if ( dy < dx ) {
            while ( a < dy ) {
                do Screen.drawHorLine(y + a, x1, x2);
                let a = a + 1;
            }
        }
        else {
            while ( a < dx ) {
                do Screen.drawVertLine(x + a, y1, y2);
                let a = a + 1;
            }
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int b, sqrt;

        if ( (x < 0 ) | (x > 511) | (y < 0) | (y > 255) ) {
            do Sys.error(12); // illegal circle center coordinates
        }

        if ( r > 181 ) {
            do Sys.error(13); // Illegal radius
        }

        let b = y - r; // top of circle
        while ( b < (y + r + 1)) {
            let sqrt = Math.sqrt( (r*r) - ((y-b)*(y-b)) );
            do Screen.drawHorLine(b, x-sqrt, x+sqrt);
            let b = b + 1;
        }
        return;
    }
}
